apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
    buildToolsVersion '27.0.3'
    compileSdkVersion 23

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // disabling lint checks because of Thrift dependency
    lintOptions {
        disable 'InvalidPackage'
        disable 'DuplicatePlatformClasses'
    }
}

repositories {
    mavenCentral()
}

ext {
    factualEngineVersion = "7.0.0"
    segmentAnalyticsVersion = "4.0.0"

    artifact = "analytics-engine"
    libraryVersion = "3.0.0"
}

dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compileOnly "com.segment.analytics.android:analytics:${segmentAnalyticsVersion}"
    compileOnly "com.factual:engine-sdk:${factualEngineVersion}"
}

// local repo
install {
    repositories {
        mavenInstaller {
            pom {
                project {
                    groupId 'com.factual.engine.analytics'
                    artifactId artifact
                    version libraryVersion

                    name 'AnalyticsEngine'
                    description 'Segment Analytics and Factual Engine Integration'
                    url 'https://github.com/Factual/segment-analytics-factual-engine-android'
                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'factual'
                            name 'Factual, Inc.'
                        }
                    }
                }
            }
        }
    }
}

// external repo
uploadArchives {
    repositories {
        def deployRepoUrl = System.getenv('DEPLOY_REPO_URL') ?: ''
        def deployRepoUser = System.getenv('DEPLOY_REPO_USER') ?: ''
        def deployRepoPass = System.getenv('DEPLOY_REPO_PASS') ?: ''

        mavenDeployer {
            repository(url: deployRepoUrl) {
                authentication(userName: deployRepoUser, password: deployRepoPass)
            }
            pom = install.repositories.mavenInstaller.pom
        }
    }
}
